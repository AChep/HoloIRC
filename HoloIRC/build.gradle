apply plugin: 'android'
apply plugin: 'android-test'

repositories {
    mavenCentral()
}

configurations {
    provided
}

android.applicationVariants.all {
    variant -> variant.javaCompile.classpath += configurations.provided
}

dependencies {
    compile 'com.google.guava:guava:15.0'
    compile 'com.android.support:support-v4:18.0.0'
    compile 'com.android.support:appcompat-v7:18.0.0'
    compile 'org.apache.commons:commons-lang3:3.0'
    compile 'com.squareup:otto:1.3.2'
    compile 'com.nineoldandroids:library:2.4.0'
    compile ('de.keyboardsurfer.android.widget:crouton:1.8.1') {
        exclude group: 'com.google.android'
    }
    compile project(':libraries:PagerSlidingTabStrip:library')
    compile project(':libraries:SlidingMenu:library')
    compile project(':libraries:StickyListHeaders:library')
    compile project(':libraries:android-numberpicker:library')
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'org.projectlombok:lombok:0.10.6'

    // TEST
    testCompile 'junit:junit:4.10'
    testCompile 'org.robolectric:robolectric:2.1.+'
    testCompile 'com.squareup:fest-android:1.0.+'
}

android {
    compileSdkVersion 18
    buildToolsVersion "17.0.0"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 18
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

// specify signing properties on the command line
if (hasProperty('storeFile')) {
    println 'Generating a signed package.'
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}